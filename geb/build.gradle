apply plugin: "groovy"
apply plugin: "idea"

repositories {
    mavenCentral()
}

// The drivers we want to use
ext.drivers = ["firefox", "chrome"]

dependencies {
    def gebVersion = "0.9.0"
    def seleniumVersion = "2.35.0"
    testCompile 'org.codehaus.groovy:groovy-all:2.0.5'

    testCompile 'junit:junit:4.11'

    testCompile "org.gebish:geb-junit4:$gebVersion"

    // Drivers
    drivers.each { driver ->
        testCompile "org.seleniumhq.selenium:selenium-$driver-driver:$seleniumVersion"
    }
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        testReportDir = reporting.file("$name/tests")
        testResultsDir = file("$buildDir/test-results/$name")

        systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
        systemProperty "geb.env", driver
        systemProperty "webdriver.chrome.driver","/home/thsea/webdriver/chromedriver"

        // If you wanted to set the baseUrl in your buildâ€¦
        systemProperty "geb.build.baseUrl", "http://thsea-uat.nfshost.com/"
    }
}

test {
        systemProperty "geb.build.reportsDir", reporting.file("geb")
        dependsOn drivers.collect { tasks["${it}Test"] }
        enabled = false
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.5'
}